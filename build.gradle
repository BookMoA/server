plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.diffplug.spotless' version '6.23.3'
}

group = 'com.umc'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'com.mysql:mysql-connector-j'

	implementation 'io.github.cdimascio:java-dotenv:5.2.2' // .env 사용
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0' // 스웨거 설정

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'io.rest-assured:rest-assured:5.3.1'

	// 스프링 시큐리티 사용시 주석 해제
	// implementation 'org.springframework.boot:spring-boot-starter-security'
	// testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('installLocalGitHook', Copy) {
	from("${rootProject.rootDir}/.github/hooks")
	into("${rootProject.rootDir}/.git/hooks")

	eachFile {
		mode = 0755
	}

	doLast {
		println 'pre-commit 훅이 성공적으로 설치되었습니다.'
	}
}

tasks.named('compileJava') {
	dependsOn 'installLocalGitHook'
}

spotless {
	java {
		// Google Java 포맷 적용
		googleJavaFormat().aosp()
		// 아래 순서로 import문 정렬
		importOrder()
		// 사용하지 않는 import 제거
		removeUnusedImports()
		// 각 라인 끝에 있는 공백을 제거
		trimTrailingWhitespace()
		// 파일 끝에 새로운 라인 추가
		endWithNewline()
	}
	format 'misc', {
		target "*.gradle", "*.md", ".gitignore"
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
}
